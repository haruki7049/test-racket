{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";
    systems.url = "github:nix-systems/default";
    flake-compat.url = "github:edolstra/flake-compat";
    flake-parts = {
      url = "github:hercules-ci/flake-parts";
      inputs.nixpkgs-lib.follows = "nixpkgs";
    };
    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs:
    inputs.flake-parts.lib.mkFlake { inherit inputs; } {
      systems = import inputs.systems;
      imports = [
        inputs.treefmt-nix.flakeModule
      ];

      perSystem =
        { pkgs, lib, ... }:
        {
          treefmt = {
            projectRootFile = ".git/config";

            # Nix
            programs.nixfmt.enable = true;

            # Markdown
            programs.mdformat.enable = true;

            # ShellScript
            programs.shellcheck.enable = true;
            programs.shfmt.enable = true;
          };

          devShells.default = pkgs.mkShell rec {
            nativeBuildInputs = [
              pkgs.racket-minimal
            ];

            buildInputs = [
              pkgs.fontconfig.lib
              pkgs.zlib
              pkgs.cairo
              pkgs.libpng
              pkgs.libjpeg
              pkgs.glib.out
              pkgs.pango
            ];

            LD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
            DYLD_LIBRARY_PATH = lib.makeLibraryPath buildInputs;
          };
        };
    };
}
